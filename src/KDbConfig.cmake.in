# KDbConfig.cmake provides information about the installed KDb library.
# It can be used directly from CMake via find_package(KDb NO_MODULE)
#
# Any changes in this ".cmake" file will be overwritten by CMake, the source is the ".cmake.in" file.
#
# The following CMake variables are provided:
#   KDb_VERSION_MAJOR - the major version number of KDb
#   KDb_VERSION_MINOR - the minor version number of KDb
#   KDb_VERSION_PATCH - the patch version number of KDb
#   KDb_INCLUDE_DIRS  - the include directories to use
#
# Additionally, the following imported library targets are created, which may be used directly
# with target_link_libraries():
#   KDb - the KDb library
#
# Provided features are available under KDB_* variables at the end of this file.

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

find_dependency(Qt@QT_MAJOR_VERSION@Core @REQUIRED_QT_VERSION@)
find_dependency(Qt@QT_MAJOR_VERSION@Widgets @REQUIRED_QT_VERSION@)
find_dependency(Qt@QT_MAJOR_VERSION@Xml @REQUIRED_QT_VERSION@)
find_dependency(KF@QT_MAJOR_VERSION@CoreAddons @REQUIRED_KF_VERSION@)

include("${CMAKE_CURRENT_LIST_DIR}/KDbTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/KDbQCHTargets.cmake")

@PACKAGE_SETUP_AUTOMOC_VARIABLES@

# Features
set(KDB_DEBUG_GUI @KDB_DEBUG_GUI@)

# Match COMPONENTS to features (KDB_ prefixes)
#message(status " KDb_FIND_COMPONENTS=${KDb_FIND_COMPONENTS}")
foreach (_COMP ${KDb_FIND_COMPONENTS})
    if(NOT DEFINED KDB_${_COMP} OR NOT ${KDB_${_COMP}})
        message(FATAL_ERROR "KDb does not contain required component ${_COMP}. Update KDb or remove this requirement.")
    endif()
endforeach()

include(FeatureSummary)
set_package_properties(KDb PROPERTIES
                       DESCRIPTION "Database connectivity and creation framework"
                       URL "https://community.kde.org/KDb")
